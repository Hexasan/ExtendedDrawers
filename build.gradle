plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
}

def run = System.env.GITHUB_RUN_NUMBER
def isDev = System.env.DEV_BUILD == "true"

version project.mod_version + (isDev ? "-dev.$run" : "") + "+mc.$minecraft_version"
group project.maven_group

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jitpack.io" }
    maven { url "https://kneelawk.com/maven/" }
    maven { url "https://maven.blamejared.com" }
}

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

    // Dependencies
    include modImplementation("com.github.mattidragon:mconfig:$mconfig_version")
    include modImplementation("com.kneelawk:graphlib:$graphlib_version")
    modImplementation "vazkii.patchouli:Patchouli:1.18.2-71.1-FABRIC"

    // Mixin Extras
    include implementation("com.github.LlamaLad7:MixinExtras:0.0.10")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:0.0.10")
}

loom {
    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.strict-validation"

            ideConfigGenerated = true

            runDir "build/datagen/run"
        }
    }
}

task cleanDatagen(type: Delete) {
    delete "build/datagen/generated"
}

task copyDatagen(type: Copy) {
    from "src/main/generated"
    into "build/datagen/generated"

    exclude "README.md"
    exclude ".cache"

    dependsOn tasks.cleanDatagen
    dependsOn tasks.runDatagen
}

clean {
    delete "src/main/generated"
}

processResources {
    inputs.property "version", version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": version
    }
    filesMatching("**/assets/*/lang/*.json") {
        expand "version": project.mod_version
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    from "build/datagen/generated"
    dependsOn tasks.copyDatagen
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {}
}
